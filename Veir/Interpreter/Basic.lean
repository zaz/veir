module

public import Veir.RuntimeValue
public import Veir.IR.WellFormed
public import Veir.GlobalOpInfo

import Veir.Data.Comb.Basic
import Veir.Data.HW.Basic
import Veir.Data.Casting
import Veir.Interfaces.FunctionInterfaces

public section

open Veir.Data
/-!
  # Veir Interpreter

  This file contains a simple interpreter for a subset of the Veir IR.

  The interpreter maintains a mapping from IR values (`ValuePtr`) to runtime
  values (`UInt64`). Each supported operation reads its operands from this
  mapping and writes its results back into it.

  The interpreter walks the linked list of operations in a block. It continues
  until a `func.return` is encountered, at which point the returned values are
  collected and propagated to the caller.
-/

namespace Veir

variable {OpInfo : Type} [HasOpInfo OpInfo]
variable {ctx : WfIRContext OpInfo}

namespace FeltSemantics

/-- Resolve the modulus of an LLZK built-in field. -/
def prime? (type : FeltType) : Option Nat :=
  match type.fieldName with
  | none => none
  | some name =>
    if name = "bn254".toUTF8 then
      some 21888242871839275222246405745257275088548364400416034343698204186575808495617
    else if name = "bn128".toUTF8 then
      some 21888242871839275222246405745257275088548364400416034343698204186575808495617
    else if name = "grumpkin".toUTF8 then
      some 21888242871839275222246405745257275088696311157297823662689037894645226208583
    else if name = "babybear".toUTF8 then some 2013265921
    else if name = "goldilocks".toUTF8 then some 18446744069414584321
    else if name = "mersenne31".toUTF8 then some 2147483647
    else if name = "koalabear".toUTF8 then some 2130706433
    else none

/-- Whether `value` is the canonical representative of an element of `type`.
    Unknown and unnamed fields remain uninterpreted. -/
def IsCanonical (type : FeltType) (value : Nat) : Prop :=
  match prime? type with
  | some p => value < p
  | none => False

instance (type : FeltType) (value : Nat) : Decidable (IsCanonical type value) :=
  match h : prime? type with
  | none => isFalse (by simp [IsCanonical, h])
  | some p =>
    if hvalue : value < p then
      isTrue (by simp [IsCanonical, h, hvalue])
    else
      isFalse (by simp [IsCanonical, h, hvalue])

/-- Reduce an integer to its canonical representative modulo `p`. -/
def reduce (p : Nat) (value : Int) : Nat :=
  (value % (p : Int)).toNat

/-- Addition of canonical field representatives. -/
def add (p lhs rhs : Nat) : Nat :=
  (lhs + rhs) % p

/-- Subtraction of field representatives, returning a canonical representative. -/
def sub (p lhs rhs : Nat) : Nat :=
  reduce p (Int.ofNat lhs - Int.ofNat rhs)

/-- Multiplication of canonical field representatives. -/
def mul (p lhs rhs : Nat) : Nat :=
  (lhs * rhs) % p

/-- Negation of a field representative, returning a canonical representative. -/
def neg (p value : Nat) : Nat :=
  reduce p (-Int.ofNat value)

end FeltSemantics

namespace RuntimeValue

/--
  A predicate indicating whether a `RuntimeValue` is a value that is a runtime value
  of a given `TypeAttr`.
-/
@[expose]
def Conforms (val : RuntimeValue) (ty : TypeAttr) : Prop :=
  match val, ty with
  | .int bw _, ⟨.integerType intType, _⟩ => intType.bitwidth = bw
  | .float type _, ⟨.floatType floatType, _⟩ => floatType = type
  | .byte bw _, ⟨.byteType byteType, _⟩ => byteType.bitwidth = bw
  | .int bw _, ⟨.modArithType modArithType, _⟩ => modArithType.modulus.type.bitwidth = bw
  | .reg _, ⟨.registerType _, _⟩ => True
  | .addr _, ⟨.llvmPointerType _, _⟩ => True
  | .felt fieldType value, ⟨.feltType expectedType, _⟩ =>
    fieldType = expectedType ∧ FeltSemantics.IsCanonical fieldType value
  | _, _ => False

instance : Decidable (Conforms val ty) := by
  unfold Conforms
  split <;> infer_instance

@[grind <=]
theorem Conforms.integerType :
    Conforms runtimeValue ⟨.integerType intType, h⟩ →
    ∃ val, runtimeValue = .int intType.bitwidth val := by
  simp only [Conforms]
  cases runtimeValue
  case int bw val =>
    simp only [int.injEq, exists_and_left]
    intro _; subst bw
    grind
  all_goals grind

@[grind <=]
theorem Conforms.byteType {runtimeValue byteType h} :
    Conforms runtimeValue ⟨.byteType byteType, h⟩ →
    ∃ val, runtimeValue = .byte byteType.bitwidth val := by
  simp only [Conforms]
  cases runtimeValue
  case byte bw val =>
    simp only [byte.injEq, exists_and_left]
    intro _; subst bw
    grind
  all_goals grind

@[grind <=]
theorem Conforms.floatType :
    Conforms runtimeValue ⟨.floatType fltType, h⟩ →
    ∃ val, runtimeValue = .float fltType val := by
  simp only [Conforms]
  cases runtimeValue
  case float bw val =>
    simp only [float.injEq, exists_and_left]
    intro _; subst bw
    grind
  all_goals grind

@[grind <=]
theorem Conforms.modArithType {runtimeValue modArithType h} :
    Conforms runtimeValue ⟨.modArithType modArithType, h⟩ →
    ∃ val, runtimeValue = .int modArithType.modulus.type.bitwidth val := by
  simp only [Conforms]
  cases runtimeValue
  case int bw val =>
    simp only [int.injEq, exists_and_left]
    intro _; subst bw
    grind
  all_goals grind

@[grind <=]
theorem Conforms.registerType :
    Conforms runtimeValue ⟨.registerType regType, h⟩ →
    ∃ val, runtimeValue = .reg val := by
  simp only [Conforms]
  cases runtimeValue <;> grind

@[grind <=]
theorem Conforms.llvmPointerType :
    Conforms runtimeValue ⟨.llvmPointerType _, h⟩ →
    ∃ val, runtimeValue = .addr val := by
  simp only [Conforms]
  cases runtimeValue <;> grind

@[grind <=]
theorem Conforms.feltType {runtimeValue feltTy h} :
    Conforms runtimeValue ⟨.feltType feltTy, h⟩ →
    ∃ val, runtimeValue = .felt feltTy val := by
  cases runtimeValue <;> simp_all [Conforms]

/--
  The wholly-poisoned `RuntimeValue` of type `ty`, for the types that have one.
  Used to materialize a result for an operation whose evaluation triggers UB.
-/
def getPoisonForType (ty : TypeAttr) : Option RuntimeValue :=
  match ty.val with
  | .integerType intTy => some (.int intTy.bitwidth .poison)
  | .byteType byteTy => some (.byte byteTy.bitwidth LLVM.Byte.allPoison)
  | _ => none

def ArrayConforms (source : Array RuntimeValue) (target : Array TypeAttr) : Prop :=
  source.size = target.size ∧ ∀ (i : Nat) (_ : i < source.size), source[i]!.Conforms target[i]!

theorem ArrayConforms.take_succ_eq {source : Array RuntimeValue} {target : Array TypeAttr} :
    source.size = target.size →
    n < source.size →
    (ArrayConforms (source.take (n + 1)) (target.take (n + 1)) ↔
    (ArrayConforms (source.take n) (target.take n) ∧ (source[n]!).Conforms target[n]!)) := by
  simp only [ArrayConforms]
  intro hsize hn
  constructor
  · rintro ⟨_, h⟩
    constructor
    · constructor; grind
      intro i hi
      grind [h i]
    · grind [h n]
  · rintro ⟨⟨_, h⟩, hn⟩
    constructor; grind
    intro i hi
    grind [h i]

end RuntimeValue

/--
  Memory state during interpretation.
  Set bits in the poison mask represent poison bits.
-/
@[ext]
structure MemoryState where
  contents : ByteArray
  poisonMask : ByteArray
  consistentSize : contents.size = poisonMask.size

def MemoryState.empty : MemoryState := {
  contents := (ByteArray.emptyWithCapacity 1024).extend 8 0xff,
  poisonMask := (ByteArray.emptyWithCapacity 1024).extend 8 0xff,
  consistentSize := (by grind)
}

def MemoryState.ensureSize (mem : MemoryState) (size : Nat) : MemoryState :=
  if mem.contents.size < size then
    ⟨mem.contents.extend (size - mem.contents.size) 0,
      mem.poisonMask.extend (size - mem.contents.size) 0xff,
      (by simp [mem.consistentSize])⟩
  else
    mem

/--
  Property that a hash map from `ValuePtr` to `RuntimeValue` conforms to the value types in the
  IR context. This is an invariant that must be maintained by the variable state of the interpreter.
-/
def VariableState.ValuesConform (state : Std.ExtHashMap ValuePtr RuntimeValue)
    (ctx : WfIRContext OpInfo) : Prop :=
  ∀ val var, (h : val ∈ state) → state[val] = var → var.Conforms (val.getType! ctx.raw)

structure VariableState (ctx : WfIRContext OpInfo) where
  variables : Std.ExtHashMap ValuePtr RuntimeValue
  conforms : VariableState.ValuesConform variables ctx
  variablesIn : ∀ val, val ∈ variables → val.InBounds ctx.raw

/--
  Create a variable state with no variables defined.
-/
def VariableState.empty (ctx : WfIRContext OpInfo) : VariableState ctx :=
  ⟨Std.ExtHashMap.emptyWithCapacity 8, by simp [VariableState.ValuesConform], by simp⟩

/--
  The state of the interpreter at a given point in time.
  It includes a mapping from IR values to their runtime values.
-/
@[ext]
structure InterpreterState (ctx : WfIRContext OpInfo) where
  variables : VariableState ctx
  memory : MemoryState

/--
  Create an interpreter state with no variables defined.
-/
def InterpreterState.empty (ctx : WfIRContext OpInfo) : InterpreterState ctx :=
  { variables := .empty ctx, memory := .empty }

/--
  Set the runtime value of a variable.
  This function dynamically checks that the runtime value conforms to the variable type, and
  return `none` otherwise.
-/
def VariableState.setVar? (state : VariableState ctx) (var : ValuePtr)
    (val : RuntimeValue) (inBounds : var.InBounds ctx.raw := by grind) :
    Option (VariableState ctx) :=
  if h : val.Conforms (var.getType! ctx.raw) then
    some ⟨state.variables.insert var val,
      by grind [VariableState.ValuesConform, cases VariableState],
      by grind [cases VariableState]⟩
  else
    none

/--
  Set the runtime value of a variable.
  This function requires a proof that the runtime value conforms to the variable type.
-/
def VariableState.setVar (state : VariableState ctx) (var : ValuePtr)
    (val : RuntimeValue) (h : val.Conforms (var.getType! ctx.raw) := by grind)
    (inBounds : var.InBounds ctx.raw := by grind) :
    VariableState ctx :=
  ⟨state.variables.insert var val,
    by grind [VariableState.ValuesConform, cases VariableState],
    by grind [cases VariableState]⟩

/--
  Get the value of a variable, if the variable exists.
-/
def VariableState.getVar? (state : VariableState ctx) (var : ValuePtr)
    : Option RuntimeValue :=
  state.variables[var]?

@[ext]
theorem VariableState.ext {s₁ s₂ : VariableState ctx} :
    (∀ var, s₁.getVar? var = s₂.getVar? var) →
    s₁ = s₂ := by
  rcases s₁; rcases s₂
  simp only [VariableState.getVar?, mk.injEq]
  grind

/--
  Get the value of the operands of an operation.
  If any operand is not in the state, return `none`.
-/
@[expose]
def VariableState.getOperandValues (state : VariableState ctx)
    (op : OperationPtr) : Option (Array RuntimeValue) := do
  (op.getOperands! ctx.raw).mapM state.getVar?

def VariableState.setResultValues?_loop (state : VariableState ctx)
    (op : OperationPtr) (resultValues : Array RuntimeValue) (i : Nat)
    (opInBounds : op.InBounds ctx.raw := by grind)
    (iInBounds : i ≤ op.getNumResults! ctx.raw := by grind)
    (hsizes : resultValues.size = op.getNumResults! ctx.raw := by grind)
    : Option (VariableState ctx) :=
  match i with
  | 0 => state
  | i + 1 => do
    let result := op.getResult i
    let value := resultValues[i]
    let newState ← state.setVar? result value
    VariableState.setResultValues?_loop newState op resultValues i

/--
  Set the values of the results of an operation.
-/
def VariableState.setResultValues? (state : VariableState ctx)
    (op : OperationPtr) (resultValues : Array RuntimeValue) (opInBounds : op.InBounds ctx.raw := by grind)
    : Option (VariableState ctx) :=
  if hsize : resultValues.size = op.getNumResults! ctx.raw then
    VariableState.setResultValues?_loop state op resultValues (op.getNumResults! ctx.raw)
  else
    none

/--
  Implementation loop for setting the values of block arguments.
-/
def VariableState.setArgumentValues?_loop (state : VariableState ctx)
    (block : BlockPtr) (values : Array RuntimeValue) (i : Nat)
    (blockInBounds : block.InBounds ctx.raw := by grind)
    (iInBounds : i ≤ block.getNumArguments! ctx.raw := by grind)
    : Option (VariableState ctx) :=
  match i with
  | 0 => state
  | i + 1 => do
    let arg := block.getArgument i
    let value := values[i]!
    let newState ← state.setVar? arg value
    VariableState.setArgumentValues?_loop newState block values i

/--
  Set the values of block arguments.
-/
def VariableState.setArgumentValues? (state : VariableState ctx)
    (block : BlockPtr) (values : Array RuntimeValue)
    (blockInBounds : block.InBounds ctx.raw := by grind)
    : Option (VariableState ctx) :=
  VariableState.setArgumentValues?_loop state block values (block.getNumArguments! ctx.raw)

/--
  How the control flow should proceed after interpreting a terminator.
  - `return` indicates that the current block should return with the given values.
  - `branch` indicates that the interpreter should jump to another block
-/
inductive ControlFlowAction where
  | return (vals : Array RuntimeValue)
  | branch (vals : Array RuntimeValue) (dest : BlockPtr)

/--
  The interpreter monad. An interpretation step has three outcomes. UB is a property
  of the execution, not of any value, so it lives here rather than inside
  `RuntimeValue` or `LLVM.Int`.
-/
inductive Interp (α : Type) where
  /-- Interpreter could not proceed (malformed IR, unsupported op). -/
  | fail
  /-- Execution triggered undefined behaviour. -/
  | ub
  /-- Successful execution producing `a`. -/
  | ok (a : α)
deriving Inhabited

@[expose]
def Interp.map {α β : Type} (f : α → β) : Interp α → Interp β
  | .fail => .fail
  | .ub => .ub
  | .ok a => .ok (f a)

@[simp, grind =] theorem Interp.map_fail : Interp.map f .fail = .fail := rfl
@[simp, grind =] theorem Interp.map_ub : Interp.map f .ub = .ub := rfl
@[simp, grind =] theorem Interp.map_ok : Interp.map f (.ok a) = .ok (f a) := rfl

instance : Monad Interp where
  pure x := .ok x
  bind x f := match x with
    | .fail => .fail
    | .ub => .ub
    | .ok a => f a

instance : MonadLift Option Interp where
  monadLift
    | none => .fail
    | some v => .ok v

@[simp, grind =] theorem Interp.pure_eq (a : α) : (pure a : Interp α) = .ok a := rfl
@[simp, grind =] theorem Interp.bind_ok (a : α) (f : α → Interp β) :
    (Interp.ok a >>= f) = f a := rfl
@[simp, grind =] theorem Interp.bind_ub (f : α → Interp β) :
    ((.ub : Interp α) >>= f) = .ub := rfl
@[simp, grind =] theorem Interp.bind_fail (f : α → Interp β) :
    ((.fail : Interp α) >>= f) = .fail := rfl
@[simp, grind =] theorem Interp.liftOption_none : ((none : Option α) : Interp α) = .fail := rfl
@[simp, grind =] theorem Interp.liftOption_some (a : α) : ((some a : Option α) : Interp α) = .ok a := rfl


/--
  Signal UB if the divisor `b` of an unsigned division or remainder could be
  zero. A poison divisor may refine to zero, so it is immediate UB just like a
  concretely-zero one.
-/
@[inline] def Interp.checkUnsignedDivision {w : Nat} (b : LLVM.Int w) : Interp Unit :=
  if b = .poison ∨ b = .val 0 then Interp.ub else pure ()

/--
  Signal UB if the signed division or remainder `a / b` could be undefined:
  a zero divisor, or the `intMin / -1` overflow case. As above, poison operands
  may refine to any value, so they count as possibly triggering either case.
-/
@[inline] def Interp.checkSignedDivision {w : Nat} (a b : LLVM.Int w) : Interp Unit := do
  Interp.checkUnsignedDivision b
  -- The divisor is now concretely nonzero, so only a concrete `-1` can overflow.
  if b = .val (-1) ∧ (a = .poison ∨ a = .val (BitVec.intMin w)) then Interp.ub

/--
  Allocate the given number of bytes of memory.
  Return the updated memory state and the freshly allocated address.
-/
def MemoryState.alloc (state : MemoryState) (size : UInt64)
    : MemoryState × UInt64 :=
  (⟨state.contents.extend size.toNat 0,
    state.poisonMask.extend size.toNat 0xff,
    by simp [state.consistentSize]⟩, state.contents.size.toUInt64)

/--
  Store raw bytes to the given address in memory,
  and set the corresponding poison bits as requested (by default, unset).
  Yields UB if the access is out of bounds.
-/
def MemoryState.store (state : MemoryState) (addr : UInt64) (val : ByteArray)
  (poison : ByteArray := ByteArray.replicate val.size 0) (h : poison.size = val.size := by grind)
    : Interp MemoryState :=
  if addr.toNat + val.size ≤ state.contents.size then
    return ⟨val.copySlice 0 state.contents addr.toNat val.size false,
      poison.copySlice 0 state.poisonMask addr.toNat val.size false,
      by
        simp [ByteArray.copySlice_eq_append, state.consistentSize, h]
      ⟩
  else
    Interp.ub

/--
  Poison the given number n of bytes, starting from the given address in memory.
  Yields UB if the access is out of bounds.
-/
def MemoryState.empoison (state : MemoryState) (addr : UInt64) (n : Nat)
    : Interp MemoryState :=
  if h : addr.toNat + n ≤ state.poisonMask.size then
    let mask := ByteArray.replicate n 0xff
    return ⟨state.contents,
      mask.copySlice 0 state.poisonMask addr.toNat n false,
      by
        have h' : min n mask.size = n := by grind
        have h'' : min addr.toNat state.poisonMask.size = addr.toNat := by grind
        simp [ByteArray.copySlice_eq_append, state.consistentSize, h', h'']
        grind

      ⟩
  else
    Interp.ub

/--
  Store an LLVM value to memory.
  Yields UB if the access is out of bounds or the address is 0.
-/
def MemoryState.llvmStore (state : MemoryState) (addr : UInt64) (val : RuntimeValue)
    : Interp MemoryState :=
  if addr.toNat == 0 then Interp.ub else
  match val with
  | .int 8 (.val v) => state.store addr (ByteArray.empty.push (UInt8.ofBitVec v))
  | .int 16 (.val v) => state.store addr (UInt16.ofBitVec v).toByteArrayLE
  | .int 32 (.val v) => state.store addr (UInt32.ofBitVec v).toByteArrayLE
  | .int 64 (.val v) => state.store addr (UInt64.ofBitVec v).toByteArrayLE
  | .byte 64 v => state.store addr (UInt64.ofBitVec v.val).toByteArrayLE (UInt64.ofBitVec v.poison).toByteArrayLE (by simp)
  | .int n .poison => state.empoison addr (n / 8)
  | .addr v => state.store addr v.toByteArrayLE
  | _ => none

/--
  Load raw bytes from the given memory address.
  Yields UB if the access is out of bounds.
-/
def MemoryState.load (state : MemoryState) (addr size : UInt64)
    : Interp ByteArray :=
  if addr.toNat + size.toNat <= state.contents.size then
    return state.contents.extract addr.toNat (addr + size).toNat
  else
    Interp.ub

/--
  Load bitwise poison status of the given memory address.
  Yields UB if the access is out of bounds.
-/
def MemoryState.loadPoison (state : MemoryState) (addr size : UInt64)
    : Interp ByteArray :=
  if addr.toNat + size.toNat <= state.poisonMask.size then
    return state.poisonMask.extract addr.toNat (addr + size).toNat
  else
    Interp.ub

/--
  Check if any of the `size` bytes at the given memory address `addr` is poison.
  Yields UB if the access is out of bounds.
-/
def MemoryState.hasPoison (state : MemoryState) (addr size : UInt64)
    : Interp Bool := do
  let poisonMask ← state.loadPoison addr size
  let mut poison := false
  for b in poisonMask do
    if b ≠ 0 then
      poison := true
      break
  return poison

/--
  Load an LLVM value from the given memory address.
  Yields UB if access is out of bounds or the address is 0.
-/
def MemoryState.llvmLoad (state : MemoryState) (addr : UInt64) (type : TypeAttr)
    : Interp RuntimeValue := do
  if addr == 0 then Interp.ub else
  match type.val with
  | Attribute.integerType { bitwidth := 8 } =>
      let ba ← state.load addr 1
      if ← state.hasPoison addr 1 then return .int 8 .poison
      return .int 8 (.val ba[0]!.toNat)
  | Attribute.integerType { bitwidth := 16 } =>
      let ba ← state.load addr 2
      if ← state.hasPoison addr 2 then return .int 16 .poison
      return .int 16 (.val (ba.toBitVecLE 2))
  | Attribute.integerType { bitwidth := 32 } =>
      let ba ← state.load addr 4
      if ← state.hasPoison addr 4 then return .int 32 .poison
      return .int 32 (.val (ba.toBitVecLE 4))
  | Attribute.integerType { bitwidth := 64 } =>
      let ba ← state.load addr 8
      if ← state.hasPoison addr 8 then return .int 64 .poison
      return .int 64 (.val (BitVec.ofNat 64 ba.toUInt64LE!.toNat))
  | Attribute.byteType { bitwidth := 64 } =>
      let ba ← state.load addr 8
      let baPoison ← state.loadPoison addr 8
      let poison := baPoison.toUInt64LE!.toBitVec
      return .byte 64 ⟨ba.toUInt64LE!.toBitVec &&& ~~~poison, poison, by bv_decide⟩
  | Attribute.llvmPointerType _ =>
      let ba ← state.load addr 8
      -- FIXME poison address
      if ← state.hasPoison addr 8 then return .addr 0
      return .addr ba.toUInt64LE!
  | _ => none



def Arith.interpretOp' (opType : Veir.Arith) (properties : propertiesOf opType)
    (resultTypes : Array TypeAttr) (operands : Array RuntimeValue) (_blockOperands : Array BlockPtr)
    : Interp ((Array RuntimeValue) × Option ControlFlowAction) :=
  match opType with
  | .constant => do
    let some resType := resultTypes[0]? | none
    let .integerType bw := resType.val
      | none
    return (#[.int bw.bitwidth
      (.val (BitVec.ofInt bw.bitwidth properties.value.value))], none)
  | .addi => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.add lhs rhs properties.attr.nsw properties.attr.nuw)], none)
  | .subi => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.sub lhs rhs properties.attr.nsw properties.attr.nuw)], none)
  | .muli => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.mul lhs rhs properties.attr.nsw properties.attr.nuw)], none)
  | .divui => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    Interp.checkUnsignedDivision rhs
    return (#[.int bw (LLVM.Int.udiv lhs rhs properties.exact)], none)
  | .divsi => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    Interp.checkSignedDivision lhs rhs
    return (#[.int bw (LLVM.Int.sdiv lhs rhs properties.exact)], none)
  | .remui => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    Interp.checkUnsignedDivision rhs
    return (#[.int bw (LLVM.Int.urem lhs rhs)], none)
  | .remsi => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    Interp.checkSignedDivision lhs rhs
    return (#[.int bw (LLVM.Int.srem lhs rhs)], none)
  | .shli => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.shl lhs rhs properties.attr.nsw properties.attr.nuw)], none)
  | .shrsi => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.ashr lhs rhs properties.exact)], none)
  | .shrui => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.lshr lhs rhs properties.exact)], none)
  | .andi => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.and lhs rhs)], none)
  | .ori => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.or lhs rhs properties.disjoint)], none)
  | .xori => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.xor lhs rhs)], none)
  | .trunci => do
    let [.int w val] := operands.toList | none
    let some resType := resultTypes[0]? | none
    let .integerType resBw := resType.val | none
    if h: resBw.bitwidth >= w then none else
    return (#[.int resBw.bitwidth (LLVM.Int.trunc val resBw.bitwidth properties.attr.nsw properties.attr.nuw (by omega))], none)
  | .extui => do
    let [.int w val] := operands.toList | none
    let some resType := resultTypes[0]? | none
    let .integerType resBw := resType.val | none
    if h: resBw.bitwidth <= w then none else
    return (#[.int resBw.bitwidth (LLVM.Int.zext val resBw.bitwidth properties.nneg (by omega))], none)
  | .extsi => do
    let [.int w val] := operands.toList | none
    let some resType := resultTypes[0]? | none
    let .integerType resBw := resType.val | none
    if h: resBw.bitwidth <= w then none else
    return (#[.int resBw.bitwidth (LLVM.Int.sext val resBw.bitwidth (by omega))], none)
  | .select => do
    let [.int 1 cond, .int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simpa using h)
    return (#[.int bw (LLVM.Int.select cond lhs rhs)], none)
  | .cmpi => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    -- `arith.cmpi` lowers to `llvm.icmp`; the arith and LLVM predicate encodings
    -- coincide, so `properties.predicate` is used directly. Result is `i1`.
    return (#[.int 1 (LLVM.Int.icmp lhs rhs properties.predicate)], none)
  | .maxsi => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.smax lhs rhs)], none)
  | .minsi => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.smin lhs rhs)], none)
  | .maxui => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.umax lhs rhs)], none)
  | .minui => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.umin lhs rhs)], none)
  | .addui_extended => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    -- Two results: the `w`-bit sum, then the `i1` unsigned-overflow flag.
    return (#[.int bw (LLVM.Int.add lhs rhs),
              .int 1 (LLVM.Int.uaddOverflowFlag lhs rhs)], none)
  | .subui_extended => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    -- Two results: the `w`-bit difference, then the `i1` borrow flag, which is
    -- set exactly when `lhs <u rhs`.
    return (#[.int bw (LLVM.Int.sub lhs rhs),
              .int 1 (LLVM.Int.usubOverflowFlag lhs rhs)], none)
  | .mulsi_extended => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    -- Two results: the low half (same as `muli`), then the signed high half.
    return (#[.int bw (LLVM.Int.mul lhs rhs),
              .int bw (LLVM.Int.smulHigh lhs rhs)], none)
  | .mului_extended => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    -- Two results: the low half (same as `muli`), then the unsigned high half.
    return (#[.int bw (LLVM.Int.mul lhs rhs),
              .int bw (LLVM.Int.umulHigh lhs rhs)], none)
  | .ceildivui => do
    let [.int bw a, .int bw' b] := operands.toList | none
    if h: bw' ≠ bw then none else
    let b := b.cast (by simp at h; exact h)
    -- Lowering (arith ExpandOps): `a == 0 ? 0 : ((a - 1) udiv b) + 1`. The
    -- `udiv` makes a zero (or poison) divisor undefined behaviour, exactly as
    -- for `arith.divui`.
    Interp.checkUnsignedDivision b
    let zero : LLVM.Int bw := .val 0
    let one : LLVM.Int bw := .val 1
    let isZero := LLVM.Int.icmp a zero .eq
    let quotient := LLVM.Int.udiv (LLVM.Int.sub a one) b
    let plusOne := LLVM.Int.add quotient one
    return (#[.int bw (LLVM.Int.select isZero zero plusOne)], none)
  | .ceildivsi => do
    let [.int bw a, .int bw' b] := operands.toList | none
    if h: bw' ≠ bw then none else
    let b := b.cast (by simp at h; exact h)
    -- Lowering (arith ExpandOps): `z = a sdiv b;`
    -- `(a != z*b) && ((a<0) == (b<0)) ? z + 1 : z`. The intermediate `mul`/`add`
    -- carry no overflow flags (they wrap).
    let zero : LLVM.Int bw := .val 0
    let one : LLVM.Int bw := .val 1
    -- UB gating mirrors `arith.divsi` (divide-by-zero, INT_MIN / -1).
    Interp.checkSignedDivision a b
    let z := LLVM.Int.sdiv a b
    let notExact := LLVM.Int.icmp a (LLVM.Int.mul z b) .ne
    let signEqual := LLVM.Int.icmp (LLVM.Int.icmp a zero .slt) (LLVM.Int.icmp b zero .slt) .eq
    let cond := LLVM.Int.and notExact signEqual
    return (#[.int bw (LLVM.Int.select cond (LLVM.Int.add z one) z)], none)
  | .floordivsi => do
    let [.int bw a, .int bw' b] := operands.toList | none
    if h: bw' ≠ bw then none else
    let b := b.cast (by simp at h; exact h)
    -- Lowering (arith ExpandOps): `z = a sdiv b;`
    -- `(a != z*b) && ((a<0) != (b<0)) ? z - 1 : z`. The intermediate `mul`/`add`
    -- carry no overflow flags (they wrap).
    let zero : LLVM.Int bw := .val 0
    let negOne : LLVM.Int bw := .val (BitVec.allOnes bw)
    -- UB gating mirrors `arith.divsi` (divide-by-zero, INT_MIN / -1).
    Interp.checkSignedDivision a b
    let z := LLVM.Int.sdiv a b
    let notExact := LLVM.Int.icmp a (LLVM.Int.mul z b) .ne
    let signOpposite := LLVM.Int.icmp (LLVM.Int.icmp a zero .slt) (LLVM.Int.icmp b zero .slt) .ne
    let cond := LLVM.Int.and notExact signOpposite
    return (#[.int bw (LLVM.Int.select cond (LLVM.Int.add z negOne) z)], none)


/-- Matches two integer operands and casts them to the expected bitwidth `bw`. -/
private def ModArith.binaryOperands (bw : Nat) (operands : Array RuntimeValue) :
    Option (LLVM.Int bw × LLVM.Int bw) := do
  let [RuntimeValue.int bw' lhs, RuntimeValue.int bw'' rhs] := operands.toList | none
  if h : bw' = bw ∧ bw'' = bw then
    return (lhs.cast h.left, rhs.cast h.right)
  else
    none

def ModArith.interpretOp' (opType : Veir.Mod_Arith) (properties : propertiesOf opType)
    (resultTypes : Array TypeAttr) (operands : Array RuntimeValue) (_blockOperands : Array BlockPtr)
    : Interp ((Array RuntimeValue) × Option ControlFlowAction) :=
  match opType with
  | .constant => do
    let some resType := resultTypes[0]? | none
    let .modArithType ⟨⟨mod, ⟨bw⟩⟩⟩ := resType.val | none
    let res := LLVM.Int.constant bw (properties.value.value % mod)
    return (#[RuntimeValue.int bw res], none)
  | .add => do
    let some resType := resultTypes[0]? | none
    let .modArithType ⟨⟨mod, ⟨bw⟩⟩⟩ := resType.val | none
    let some (lhs, rhs) := ModArith.binaryOperands bw operands | none
    let res :=
      match lhs.toNat?, rhs.toNat? with
      | some lhs, some rhs => LLVM.Int.constant bw ((lhs + rhs) % mod)
      | _, _ => LLVM.Int.poison
    return (#[RuntimeValue.int bw res], none)
  | .sub => do
    let some resType := resultTypes[0]? | none
    let .modArithType ⟨⟨mod, ⟨bw⟩⟩⟩ := resType.val | none
    let some (lhs, rhs) := ModArith.binaryOperands bw operands | none
    let res :=
      match lhs.toNat?, rhs.toNat? with
      | some lhs, some rhs => LLVM.Int.constant bw ((Int.ofNat lhs - rhs) % mod)
      | _, _ => LLVM.Int.poison
    return (#[RuntimeValue.int bw res], none)
  | .mul => do
    let some resType := resultTypes[0]? | none
    let .modArithType ⟨⟨mod, ⟨bw⟩⟩⟩ := resType.val | none
    let some (lhs, rhs) := ModArith.binaryOperands bw operands | none
    let res :=
      match lhs.toNat?, rhs.toNat? with
      | some lhs, some rhs => LLVM.Int.constant bw ((lhs * rhs) % mod)
      | _, _ => LLVM.Int.poison
    return (#[RuntimeValue.int bw res], none)


/-- Match two felt operands whose field type is exactly `fieldType`. -/
private def Felt.binaryOperands (fieldType : FeltType) (operands : Array RuntimeValue) :
    Option (Nat × Nat) := do
  let [RuntimeValue.felt lhsType lhs, RuntimeValue.felt rhsType rhs] := operands.toList
    | none
  guard (lhsType = fieldType ∧ rhsType = fieldType)
  return (lhs, rhs)

/-- Match one felt operand whose field type is exactly `fieldType`. -/
private def Felt.unaryOperand (fieldType : FeltType) (operands : Array RuntimeValue) :
    Option Nat := do
  let [RuntimeValue.felt operandType operand] := operands.toList | none
  guard (operandType = fieldType)
  return operand

/-- Resolve the named field carried by the unique Felt result type. -/
private def Felt.resultField? (resultTypes : Array TypeAttr) : Option (FeltType × Nat) := do
  let [⟨.feltType fieldType, _⟩] := resultTypes.toList | none
  let prime ← FeltSemantics.prime? fieldType
  return (fieldType, prime)

/-- Interpret the field-native Felt operations supported by the core interpreter. -/
def Felt.interpretOp' (opType : Veir.Felt) (properties : propertiesOf opType)
    (resultTypes : Array TypeAttr) (operands : Array RuntimeValue)
    (_blockOperands : Array BlockPtr) :
    Interp (Array RuntimeValue × Option ControlFlowAction) :=
  match opType with
  | .const => do
    let (fieldType, prime) ← Felt.resultField? resultTypes
    if properties.value.fieldType ≠ fieldType then none else
      let value := FeltSemantics.reduce prime properties.value.value
      return (#[.felt fieldType value], none)
  | .add => do
    let (fieldType, prime) ← Felt.resultField? resultTypes
    let (lhs, rhs) ← Felt.binaryOperands fieldType operands
    return (#[.felt fieldType (FeltSemantics.add prime lhs rhs)], none)
  | .sub => do
    let (fieldType, prime) ← Felt.resultField? resultTypes
    let (lhs, rhs) ← Felt.binaryOperands fieldType operands
    return (#[.felt fieldType (FeltSemantics.sub prime lhs rhs)], none)
  | .mul => do
    let (fieldType, prime) ← Felt.resultField? resultTypes
    let (lhs, rhs) ← Felt.binaryOperands fieldType operands
    return (#[.felt fieldType (FeltSemantics.mul prime lhs rhs)], none)
  | .neg => do
    let (fieldType, prime) ← Felt.resultField? resultTypes
    let operand ← Felt.unaryOperand fieldType operands
    return (#[.felt fieldType (FeltSemantics.neg prime operand)], none)
  | _ => none


def Llvm.interpretOp' (opType : Veir.Llvm) (properties : propertiesOf opType)
    (resultTypes : Array TypeAttr) (operands : Array RuntimeValue) (blockOperands : Array BlockPtr)
    (mem : MemoryState)
    : Interp ((Array RuntimeValue) × MemoryState × Option ControlFlowAction) :=
  match opType with
  | .mlir__constant => do
    let some resType := resultTypes[0]? | none
    match properties.value with
    | .integer intAttr =>
      let .integerType bw := resType.val
        | none
      let origbw := intAttr.type.bitwidth
      let rawbits := BitVec.ofInt origbw intAttr.value
      let extended := match origbw with
        | 1 => rawbits.zeroExtend bw.bitwidth
        | _ => rawbits.signExtend bw.bitwidth
      return (#[.int bw.bitwidth (LLVM.Int.val extended)], mem, none)
    | .float floatAttr =>
      let .floatType bw := resType.val
        | none
      return (#[.float floatAttr.type floatAttr.value], mem, none)
    | .dense denseAttr =>
      none
    | .string _ =>
      none
  | .mlir__poison => do
    let some resType := resultTypes[0]? | none
    let .integerType bw := resType.val | none
    return (#[.int bw.bitwidth (LLVM.Int.mlir_poison bw.bitwidth)], mem, none)
  | .mlir__zero => do
    let some resType := resultTypes[0]? | none
    match resType.val with
    | .integerType bw =>
      return (#[.int bw.bitwidth (LLVM.Int.val (BitVec.ofNat bw.bitwidth 0))], mem, none)
    | .llvmPointerType _ => return (#[.addr 0], mem, none)
    | _ => none
  | .add => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.add lhs rhs properties.nsw properties.nuw)], mem, none)
  | .sub => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.sub lhs rhs properties.nsw properties.nuw)], mem, none)
  | .mul => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.mul lhs rhs properties.nsw properties.nuw)], mem, none)
  | .sdiv => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    Interp.checkSignedDivision lhs rhs
    return (#[.int bw (LLVM.Int.sdiv lhs rhs properties.exact)], mem, none)
  | .udiv => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    Interp.checkUnsignedDivision rhs
    return (#[.int bw (LLVM.Int.udiv lhs rhs properties.exact)], mem, none)
  | .srem => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    Interp.checkSignedDivision lhs rhs
    return (#[.int bw (LLVM.Int.srem lhs rhs)], mem, none)
  | .urem => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    Interp.checkUnsignedDivision rhs
    return (#[.int bw (LLVM.Int.urem lhs rhs)], mem, none)
  | .shl => do
    let [lhs, .int bw' rhs] := operands.toList | none
    match lhs with
    | .int bw lhs =>
      if h: bw' ≠ bw then none else
      let rhs := rhs.cast (by simp at h; exact h)
      return (#[.int bw (LLVM.Int.shl lhs rhs properties.nsw properties.nuw)], mem, none)
    | .byte bw lhs =>
      if h: bw' ≠ bw then none else
      if properties.nsw then none else
      let rhs := rhs.cast (by simp at h; exact h)
      return (#[.byte bw (LLVM.Byte.shl lhs rhs properties.nuw)], mem, none)
    | _ => none
  | .lshr => do
    let [lhs, .int bw' rhs] := operands.toList | none
    match lhs with
    | .int bw lhs =>
      if h: bw' ≠ bw then none else
      let rhs := rhs.cast (by simp at h; exact h)
      return (#[.int bw (LLVM.Int.lshr lhs rhs properties.exact)], mem, none)
    | .byte bw lhs =>
      if h: bw' ≠ bw then none else
      let rhs := rhs.cast (by simp at h; exact h)
      return (#[.byte bw (LLVM.Byte.lshr lhs rhs properties.exact)], mem, none)
    | _ => none
  | .ashr => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.ashr lhs rhs properties.exact)], mem, none)
  | .intr__fshl => do
    let [.int bw a, .int bw' b, .int bw'' c] := operands.toList | none
    if h: bw' ≠ bw then none else
    if h'': bw'' ≠ bw then none else
    let b := b.cast (by simp at h; exact h)
    let c := c.cast (by simp at h''; exact h'')
    return (#[.int bw (LLVM.Int.fshl a b c)], mem, none)
  | .intr__fshr => do
    let [.int bw a, .int bw' b, .int bw'' c] := operands.toList | none
    if h: bw' ≠ bw then none else
    if h'': bw'' ≠ bw then none else
    let b := b.cast (by simp at h; exact h)
    let c := c.cast (by simp at h''; exact h'')
    return (#[.int bw (LLVM.Int.fshr a b c)], mem, none)
  | .intr__ctlz => do
    let [.int bw x] := operands.toList | none
    return (#[.int bw (LLVM.Int.ctlz x properties.is_zero_poison)], mem, none)
  | .intr__cttz => do
    let [.int bw x] := operands.toList | none
    return (#[.int bw (LLVM.Int.cttz x properties.is_zero_poison)], mem, none)
  | .intr__ctpop => do
    let [.int bw x] := operands.toList | none
    return (#[.int bw (LLVM.Int.ctpop x)], mem, none)
  | .intr__bswap => do
    let [.int bw x] := operands.toList | none
    return (#[.int bw (LLVM.Int.bswap x)], mem, none)
  | .intr__bitreverse => do
    let [.int bw x] := operands.toList | none
    return (#[.int bw (LLVM.Int.bitreverse x)], mem, none)
  | .and => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.and lhs rhs)], mem, none)
  | .or => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.or lhs rhs properties.disjoint)], mem, none)
  | .xor => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.xor lhs rhs)], mem, none)
  | .intr__smax => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.smax lhs rhs)], mem, none)
  | .intr__smin => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.smin lhs rhs)], mem, none)
  | .intr__umax => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.umax lhs rhs)], mem, none)
  | .intr__umin => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.umin lhs rhs)], mem, none)
  | .intr__abs => do
    let [.int bw x] := operands.toList | none
    return (#[.int bw (LLVM.Int.abs x properties.is_int_min_poison)], mem, none)
  | .intr__sadd__sat => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.saddSat lhs rhs)], mem, none)
  | .intr__uadd__sat => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.uaddSat lhs rhs)], mem, none)
  | .intr__ssub__sat => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.ssubSat lhs rhs)], mem, none)
  | .intr__usub__sat => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.usubSat lhs rhs)], mem, none)
  | .intr__sshl__sat => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.sshlSat lhs rhs)], mem, none)
  | .intr__ushl__sat => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simp at h; exact h)
    return (#[.int bw (LLVM.Int.ushlSat lhs rhs)], mem, none)
  | .trunc => do
    let [val] := operands.toList | none
    let some resType := resultTypes[0]? | none
    match val with
    | .int w val =>
        let .integerType resBw := resType.val | none
        if h: resBw.bitwidth >= w then none else
        return (#[.int resBw.bitwidth (LLVM.Int.trunc val resBw.bitwidth properties.nsw properties.nuw (by omega))], mem, none)
    | .byte w val =>
        let .byteType resBw := resType.val | none
        if h: resBw.bitwidth >= w then none else
        return (#[.byte resBw.bitwidth (LLVM.Byte.trunc val resBw.bitwidth)], mem, none)
    | _ => none
  | .zext => do
    let [.int w val] := operands.toList | none
    let some resType := resultTypes[0]? | none
    let .integerType resBw := resType.val | none
    if h: resBw.bitwidth <= w then none else
    return (#[.int resBw.bitwidth (LLVM.Int.zext val resBw.bitwidth properties.nneg (by omega))], mem, none)
  | .sext => do
    let [.int w val] := operands.toList | none
    let some resType := resultTypes[0]? | none
    let .integerType resBw := resType.val | none
    if h: resBw.bitwidth <= w then none else
    return (#[.int resBw.bitwidth (LLVM.Int.sext val resBw.bitwidth (by omega))], mem, none)
  | .icmp => do
    let [.int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simpa using h)
    return (#[.int 1 (LLVM.Int.icmp lhs rhs properties.predicate)], mem, none)
  | .select => do
    let [.int 1 cond, .int bw lhs, .int bw' rhs] := operands.toList | none
    if h: bw' ≠ bw then none else
    let rhs := rhs.cast (by simpa using h)
    return (#[.int bw (LLVM.Int.select cond lhs rhs)], mem, none)
  | .return => do
    return (#[], mem, some (.return operands))
  | .unreachable =>
    Interp.ub
  | .br => do
    let [dest] := blockOperands.toList | none
    return (#[], mem, some (.branch operands dest))
  | .cond_br => do
    let [destTrue, destFalse] := blockOperands.toList | none
    let some condVal := operands[0]? | none
    let some (trueSizeInt : Int) := properties.operandSegmentSizes.values[1]? | none
    let trueSize := trueSizeInt.toNat
    match condVal with
    | .int 1 (.val cond) =>
      if cond = 1#1 then
        return (#[], mem, some (.branch (operands.extract 1 (trueSize + 1)) destTrue))
      else
        return (#[], mem, some (.branch (operands.extract (trueSize + 1) operands.size) destFalse))
    | .int 1 .poison => Interp.ub
    | _ => none
  | .switch => do
    let some destDefault := blockOperands[0]? | none
    let some value := operands[0]? | none
    let some (defaultSizeInt : Int) := properties.operandSegmentSizes.values[1]? | none
    let defaultSize := defaultSizeInt.toNat
    let caseSegments := properties.case_operand_segments.values
    let some caseValues := properties.caseValues? | none
    /- A case value per case, or the switch cannot be read. -/
    if caseValues.size ≠ caseSegments.size then none else
    match value with
    | .int bw (.val v) =>
      let mut base := 1 + defaultSize
      for i in [0:caseSegments.size] do
        let some (countInt : Int) := caseSegments[i]? | none
        let count := countInt.toNat
        if v = BitVec.ofInt bw caseValues[i]! then
          let some dest := blockOperands[i + 1]? | none
          return (#[], mem, some (.branch (operands.extract base (base + count)) dest))
        base := base + count
      return (#[], mem, some (.branch (operands.extract 1 (1 + defaultSize)) destDefault))
    | .int _ .poison => Interp.ub
    | _ => none
  | .alloca => do
    let [.int _ (.val count)] := operands.toList | none
    let size ← match properties.elem_type.val with
    | Attribute.integerType { bitwidth := bw } => .ok ((bw / 8))
    | .llvmPointerType _ => .ok (8)
    | _ => none
    let totalSize := (size * count.toNat).toUInt64
    let (mem, addr) := mem.alloc totalSize
    return (#[.addr addr], mem, none)
  | .load => do
    let [.addr addr] := operands.toList | none
    let [type] := resultTypes.toList | none
    let val ← mem.llvmLoad addr type
    return (#[val], mem, none)
  | .store => do
    let [val, .addr addr] := operands.toList | none
    let mem ← mem.llvmStore addr val
    return (#[], mem, none)
  | .intr__assume => do
    -- Operand bundles carry assumptions of their own (`align`, `nonnull`, ...)
    -- that are not modelled, so only the bundle-free form is interpreted.
    let [.int 1 cond] := operands.toList | none
    if cond = .val 1#1 then return (#[], mem, none) else Interp.ub
  | .getelementptr => do
    /- only supports exactly one dynamic index for now -/
    let [.addr ptr, .int _ idx] := operands.toList | none
    let size ← Attribute.sizeOfType properties.elem_type.val
    match idx with
    | .val idx => return (#[.addr (ptr.toNat + idx.toNat * size).toUInt64], mem, none)
    | .poison => Interp.ub
  | .freeze => do
    let [val] := operands.toList | none
    match val with
    | .int w val =>
        return (#[.int w val.freeze], mem, none)
    | .byte w val =>
        return (#[.byte w val.freeze], mem, none)
    | _ => none
  | .bitcast => do
    let [val] := operands.toList | none
    let [⟨type, _⟩] := resultTypes.toList | none
    let result ← do match val, type with
      | .int bw1 val', .integerType ⟨bw2⟩ =>
          if bw1 ≠ bw2 then .fail else .ok (val)
      | .int bw1 val', .byteType ⟨bw2⟩ =>
          if bw1 ≠ bw2 then .fail else .ok ((.byte bw1 $ LLVM.Byte.fromInt val'))
      | .byte bw1 val', .byteType ⟨bw2⟩ =>
          if bw1 ≠ bw2 then .fail else .ok (val)
      | .byte bw1 val', .integerType ⟨bw2⟩ =>
          if bw1 ≠ bw2 then .fail else .ok ((.int bw1 $ val'.toInt))
      | .byte bw val', .llvmPointerType _ =>
          if h : bw = 64 then .ok ((.addr (val'.cast h).toUInt64)) else .fail
      | .addr val', .llvmPointerType _ => .ok (val)
      | .addr val', .byteType ⟨bw⟩ =>
          if h : bw = 64 then .ok ((.byte 64 $ LLVM.Byte.fromUInt64 val')) else .fail
      | _, _ => none
    return (#[result], mem, none)
  | _ => none

/-- Effective address of a RISC-V load/store: the base register value plus the
    sign-extended 12-bit immediate offset. -/
def riscvEffectiveAddr (base : BitVec 64) (offset : Int) : BitVec 64 :=
  base + (BitVec.ofInt 12 offset).signExtend 64

/-- For RISC-V sub-register loads. -/
inductive LoadExtension
  | signExt
  | zeroExt

/-- Read `bytes` of little-endian data from memory starting at
    `eaddr` and extend it to 64 bits according to `ext`. Memory is
    grown so that the access is in bounds and cannot raise UB. -/
def riscvLoad (mem : MemoryState) (eaddr : BitVec 64) (bytes : Nat) (ext : LoadExtension) :
    Interp (BitVec 64 × MemoryState) := do
  let mem := mem.ensureSize (eaddr.toNat + bytes)
  let ba ← mem.load eaddr.toNat.toUInt64 bytes.toUInt64
  let val := ba.toBitVecLE bytes
  let extended := match ext with
    | .signExt => val.signExtend 64
    | .zeroExt => val.setWidth 64
  return (extended, mem)

def Riscv.interpretOp' (opType : Veir.Riscv) (properties : propertiesOf opType)
    (_resultTypes : Array TypeAttr) (operands : Array RuntimeValue) (_blockOperands : Array BlockPtr)
    (mem : MemoryState)
    : Interp ((Array RuntimeValue) × MemoryState × Option ControlFlowAction) :=
  match opType with
  | .li => do
    let imm := BitVec.ofInt 64 properties.value.value
    return (#[.reg (RISCV.li imm)], mem, none)
  | .lui => do
    let imm := BitVec.ofInt 20 properties.value.value
    return (#[.reg (RISCV.lui imm)], mem, none)
  | .auipc => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 20 properties.value.value
    return (#[.reg (RISCV.auipc imm op)], mem, none)
  | .addi => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 12 properties.value.value
    return (#[.reg (RISCV.addi imm op)], mem, none)
  | .slti => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 12 properties.value.value
    return (#[.reg (RISCV.slti imm op)], mem, none)
  | .sltiu => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 12 properties.value.value
    return (#[.reg (RISCV.sltiu imm op)], mem, none)
  | .andi => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 12 properties.value.value
    return (#[.reg (RISCV.andi imm op)], mem, none)
  | .ori => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 12 properties.value.value
    return (#[.reg (RISCV.ori imm op)], mem, none)
  | .xori => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 12 properties.value.value
    return (#[.reg (RISCV.xori imm op)], mem, none)
  | .addiw => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 12 properties.value.value
    return (#[.reg (RISCV.addiw imm op)], mem, none)
  | .slli => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 6 properties.value.value
    return (#[.reg (RISCV.slli imm op)], mem, none)
  | .srli => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 6 properties.value.value
    return (#[.reg (RISCV.srli imm op)], mem, none)
  | .srai => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 6 properties.value.value
    return (#[.reg (RISCV.srai imm op)], mem, none)
  | .add => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.add op2 op1)], mem, none)
  | .sub => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.sub op2 op1)], mem, none)
  | .sll => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.sll op2 op1)], mem, none)
  | .slt => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.slt op2 op1)], mem, none)
  | .sltu => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.sltu op2 op1)], mem, none)
  | .xor => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.xor op2 op1)], mem, none)
  | .srl => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.srl op2 op1)], mem, none)
  | .sra => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.sra op2 op1)], mem, none)
  | .or => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.or op2 op1)], mem, none)
  | .and => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.and op2 op1)], mem, none)
  | .slliw => do
    let [.reg op1] := operands.toList | none
    let imm := BitVec.ofInt 5 properties.value.value
    return (#[.reg (RISCV.slliw imm op1)], mem, none)
  | .srliw => do
    let [.reg op1] := operands.toList | none
    let imm := BitVec.ofInt 5 properties.value.value
    return (#[.reg (RISCV.srliw imm op1)], mem, none)
  | .sraiw => do
    let [.reg op1] := operands.toList | none
    let imm := BitVec.ofInt 5 properties.value.value
    return (#[.reg (RISCV.sraiw imm op1)], mem, none)
  | .addw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.addw op2 op1)], mem, none)
  | .subw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.subw op2 op1)], mem, none)
  | .sllw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.sllw op2 op1)], mem, none)
  | .srlw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.srlw op2 op1)], mem, none)
  | .sraw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.sraw op2 op1)], mem, none)
  | .rem => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.rem op2 op1)], mem, none)
  | .remu => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.remu op2 op1)], mem, none)
  | .remw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.remw op2 op1)], mem, none)
  | .remuw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.remuw op2 op1)], mem, none)
  | .mul => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.mul op2 op1)], mem, none)
  | .mulh => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.mulh op2 op1)], mem, none)
  | .mulhu => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.mulhu op2 op1)], mem, none)
  | .mulhsu => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.mulhsu op2 op1)], mem, none)
  | .mulw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.mulw op2 op1)], mem, none)
  | .div => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.div op2 op1)], mem, none)
  | .divw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.divw op2 op1)], mem, none)
  | .divu => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.divu op2 op1)], mem, none)
  | .divuw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.divuw op2 op1)], mem, none)
  | .adduw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.adduw op2 op1)], mem, none)
  | .sh1adduw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.sh1adduw op2 op1)], mem, none)
  | .sh2adduw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.sh2adduw op2 op1)], mem, none)
  | .sh3adduw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.sh3adduw op2 op1)], mem, none)
  | .sh1add => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.sh1add op2 op1)], mem, none)
  | .sh2add => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.sh2add op2 op1)], mem, none)
  | .sh3add => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.sh3add op2 op1)], mem, none)
  | .slliuw => do
    let [.reg op1] := operands.toList | none
    let imm := BitVec.ofInt 6 properties.value.value
    return (#[.reg (RISCV.slliuw imm op1)], mem, none)
  | .andn => do
    let [.reg op1, .reg op2,] := operands.toList | none
    return (#[.reg (RISCV.andn op2 op1)], mem, none)
  | .orn => do
    let [.reg op1, .reg op2,] := operands.toList | none
    return (#[.reg (RISCV.orn op2 op1)], mem, none)
  | .xnor => do
    let [.reg op1, .reg op2,] := operands.toList | none
    return (#[.reg (RISCV.xnor op2 op1)], mem, none)
  | .max => do
    let [.reg op1, .reg op2,] := operands.toList | none
    return (#[.reg (RISCV.max op2 op1)], mem, none)
  | .maxu => do
    let [.reg op1, .reg op2,] := operands.toList | none
    return (#[.reg (RISCV.maxu op2 op1)], mem, none)
  | .min => do
    let [.reg op1, .reg op2,] := operands.toList | none
    return (#[.reg (RISCV.min op2 op1)], mem, none)
  | .minu => do
    let [.reg op1, .reg op2,] := operands.toList | none
    return (#[.reg (RISCV.minu op2 op1)], mem, none)
  | .rol => do
    let [.reg op1, .reg op2,] := operands.toList | none
    return (#[.reg (RISCV.rol op2 op1)], mem, none)
  | .ror => do
    let [.reg op1, .reg op2,] := operands.toList | none
    return (#[.reg (RISCV.ror op2 op1)], mem, none)
  | .rolw => do
    let [.reg op1, .reg op2,] := operands.toList | none
    return (#[.reg (RISCV.rolw op2 op1)], mem, none)
  | .rorw => do
    let [.reg op1, .reg op2,] := operands.toList | none
    return (#[.reg (RISCV.rorw op2 op1)], mem, none)
  | .sextb => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.sextb op)], mem, none)
  | .sexth => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.sexth op)], mem, none)
  | .zexth => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.zexth op)], mem, none)
  | .clz => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.clz op)], mem, none)
  | .clzw => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.clzw op)], mem, none)
  | .ctz => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.ctz op)], mem, none)
  | .ctzw => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.ctzw op)], mem, none)
  | .cpop => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.cpop op)], mem, none)
  | .cpopw => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.cpopw op)], mem, none)
  | .orcb => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.orcb op)], mem, none)
  | .rev8 => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.rev8 op)], mem, none)
  | .roriw => do
    let [.reg op1] := operands.toList | none
    let imm := BitVec.ofInt 5 properties.value.value
    return (#[.reg (RISCV.roriw imm op1)], mem, none)
  | .rori => do
    let [.reg op1] := operands.toList | none
    let imm := BitVec.ofInt 6 properties.value.value
    return (#[.reg (RISCV.rori imm op1)], mem, none)
  | .bclr => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.bclr op2 op1)], mem, none)
  | .bext => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.bext op2 op1)], mem, none)
  | .binv => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.binv op2 op1)], mem, none)
  | .bset => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.bset op2 op1)], mem, none)
  | .bclri => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 6 properties.value.value
    return (#[.reg (RISCV.bclri imm op)], mem, none)
  | .bexti => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 6 properties.value.value
    return (#[.reg (RISCV.bexti imm op)], mem, none)
  | .binvi => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 6 properties.value.value
    return (#[.reg (RISCV.binvi imm op)], mem, none)
  | .bseti => do
    let [.reg op] := operands.toList | none
    let imm := BitVec.ofInt 6 properties.value.value
    return (#[.reg (RISCV.bseti imm op)], mem, none)
  | .pack => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.pack op2 op1)], mem, none)
  | .packh => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.packh op2 op1)], mem, none)
  | .packw => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.packw op2 op1)], mem, none)
  | .czeroeqz => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.czeroeqz op2 op1)], mem, none)
  | .czeronez => do
    let [.reg op1, .reg op2] := operands.toList | none
    return (#[.reg (RISCV.czeronez op2 op1)], mem, none)
  | .mv => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.mv op)], mem, none)
  | .not => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.not op)], mem, none)
  | .neg => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.neg op)], mem, none)
  | .negw => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.negw op)], mem, none)
  | .sextw => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.sextw op)], mem, none)
  | .zextb => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.zextb op)], mem, none)
  | .zextw => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.zextw op)], mem, none)
  | .seqz => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.seqz op)], mem, none)
  | .snez => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.snez op)], mem, none)
  | .sltz=> do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.sltz op)], mem, none)
  | .sgtz => do
    let [.reg op] := operands.toList | none
    return (#[.reg (RISCV.sgtz op)], mem, none)
  | .ld => do
    let [.reg addr] := operands.toList | none
    let eaddr := riscvEffectiveAddr addr.val properties.value.value
    let (val, mem) ← riscvLoad mem eaddr 8 .zeroExt
    return (#[.reg $ .mk val], mem, none)
  | .lw => do
    let [.reg addr] := operands.toList | none
    let eaddr := riscvEffectiveAddr addr.val properties.value.value
    let (val, mem) ← riscvLoad mem eaddr 4 .signExt
    return (#[.reg $ .mk val], mem, none)
  | .lwu => do
    let [.reg addr] := operands.toList | none
    let eaddr := riscvEffectiveAddr addr.val properties.value.value
    let (val, mem) ← riscvLoad mem eaddr 4 .zeroExt
    return (#[.reg $ .mk val], mem, none)
  | .lh => do
    let [.reg addr] := operands.toList | none
    let eaddr := riscvEffectiveAddr addr.val properties.value.value
    let (val, mem) ← riscvLoad mem eaddr 2 .signExt
    return (#[.reg $ .mk val], mem, none)
  | .lhu => do
    let [.reg addr] := operands.toList | none
    let eaddr := riscvEffectiveAddr addr.val properties.value.value
    let (val, mem) ← riscvLoad mem eaddr 2 .zeroExt
    return (#[.reg $ .mk val], mem, none)
  | .lb => do
    let [.reg addr] := operands.toList | none
    let eaddr := riscvEffectiveAddr addr.val properties.value.value
    let (val, mem) ← riscvLoad mem eaddr 1 .signExt
    return (#[.reg $ .mk val], mem, none)
  | .lbu => do
    let [.reg addr] := operands.toList | none
    let eaddr := riscvEffectiveAddr addr.val properties.value.value
    let (val, mem) ← riscvLoad mem eaddr 1 .zeroExt
    return (#[.reg $ .mk val], mem, none)
  | .sd => do
    let [.reg { val }, .reg addr] := operands.toList | none
    let eaddr := riscvEffectiveAddr addr.val properties.value.value
    let mem := mem.ensureSize (eaddr.toNat + 8)
    let mem ← mem.store eaddr.toNat.toUInt64 (UInt64.ofBitVec val).toByteArrayLE
    return (#[], mem, none)
  | .sw => do
    let [.reg { val }, .reg addr] := operands.toList | none
    let eaddr := riscvEffectiveAddr addr.val properties.value.value
    let mem := mem.ensureSize (eaddr.toNat + 4)
    -- store only the low 4 bytes of the register
    let mem ← mem.store eaddr.toNat.toUInt64 ((UInt64.ofBitVec val).toByteArrayLE.extract 0 4)
    return (#[], mem, none)
  | .sh => do
    let [.reg { val }, .reg addr] := operands.toList | none
    let eaddr := riscvEffectiveAddr addr.val properties.value.value
    let mem := mem.ensureSize (eaddr.toNat + 2)
    -- store only the low 2 bytes of the register
    let mem ← mem.store eaddr.toNat.toUInt64 ((UInt64.ofBitVec val).toByteArrayLE.extract 0 2)
    return (#[], mem, none)
  | .sb => do
    let [.reg { val }, .reg addr] := operands.toList | none
    let eaddr := riscvEffectiveAddr addr.val properties.value.value
    let mem := mem.ensureSize (eaddr.toNat + 1)
    -- store only the low byte of the register
    let mem ← mem.store eaddr.toNat.toUInt64 ((UInt64.ofBitVec val).toByteArrayLE.extract 0 1)
    return (#[], mem, none)

def Riscv_Stack.interpretOp' (opType : Veir.Riscv_Stack) (properties : propertiesOf opType)
    (_resultTypes : Array TypeAttr) (_operands : Array RuntimeValue) (_blockOperands : Array BlockPtr)
    (mem : MemoryState)
    : Interp ((Array RuntimeValue) × MemoryState × Option ControlFlowAction) :=
  match opType with
  | .alloca => do
    let (mem, addr) := mem.alloc properties.size.value.toNat.toUInt64
    return (#[.reg ⟨.ofNat 64 addr.toNat⟩], mem, none)

def Riscv_Cf.interpretOp' (opType : Veir.Riscv_Cf) (properties : propertiesOf opType)
    (_resultTypes : Array TypeAttr) (operands : Array RuntimeValue) (blockOperands : Array BlockPtr)
    : Interp (Array RuntimeValue × Option ControlFlowAction) :=
  match opType with
  | .branch => do
    let [dest] := blockOperands.toList | none
    return (#[], some (.branch operands dest))
  | .beq => do
    let [destTrue, destFalse] := blockOperands.toList | none
    let some (RuntimeValue.reg lhs) := operands[0]? | none
    let some (RuntimeValue.reg rhs) := operands[1]? | none
    let some trueSize := properties.operandSegmentSizes.values[2]? | none
    let trueSize := trueSize.toNat
    if lhs == rhs then
      return (#[], some (.branch (operands.extract 2 (trueSize + 2)) destTrue))
    else
      return (#[], some (.branch (operands.extract (trueSize + 2) operands.size) destFalse))
  | .bne => do
    let [destTrue, destFalse] := blockOperands.toList | none
    let some (RuntimeValue.reg lhs) := operands[0]? | none
    let some (RuntimeValue.reg rhs) := operands[1]? | none
    let some trueSize := properties.operandSegmentSizes.values[2]? | none
    let trueSize := trueSize.toNat
    if lhs != rhs then
      return (#[], some (.branch (operands.extract 2 (trueSize + 2)) destTrue))
    else
      return (#[], some (.branch (operands.extract (trueSize + 2) operands.size) destFalse))
  | .blt => do
    let [destTrue, destFalse] := blockOperands.toList | none
    let some (RuntimeValue.reg lhs) := operands[0]? | none
    let some (RuntimeValue.reg rhs) := operands[1]? | none
    let some trueSize := properties.operandSegmentSizes.values[2]? | none
    let trueSize := trueSize.toNat
    if BitVec.slt lhs.val rhs.val then
      return (#[], some (.branch (operands.extract 2 (trueSize + 2)) destTrue))
    else
      return (#[], some (.branch (operands.extract (trueSize + 2) operands.size) destFalse))
  | .bge => do
    let [destTrue, destFalse] := blockOperands.toList | none
    let some (RuntimeValue.reg lhs) := operands[0]? | none
    let some (RuntimeValue.reg rhs) := operands[1]? | none
    let some trueSize := properties.operandSegmentSizes.values[2]? | none
    let trueSize := trueSize.toNat
    if !BitVec.slt lhs.val rhs.val then
      return (#[], some (.branch (operands.extract 2 (trueSize + 2)) destTrue))
    else
      return (#[], some (.branch (operands.extract (trueSize + 2) operands.size) destFalse))
  | .bltu => do
    let [destTrue, destFalse] := blockOperands.toList | none
    let some (RuntimeValue.reg lhs) := operands[0]? | none
    let some (RuntimeValue.reg rhs) := operands[1]? | none
    let some trueSize := properties.operandSegmentSizes.values[2]? | none
    let trueSize := trueSize.toNat
    if BitVec.ult lhs.val rhs.val then
      return (#[], some (.branch (operands.extract 2 (trueSize + 2)) destTrue))
    else
      return (#[], some (.branch (operands.extract (trueSize + 2) operands.size) destFalse))
  | .bgeu => do
    let [destTrue, destFalse] := blockOperands.toList | none
    let some (RuntimeValue.reg lhs) := operands[0]? | none
    let some (RuntimeValue.reg rhs) := operands[1]? | none
    let some trueSize := properties.operandSegmentSizes.values[2]? | none
    let trueSize := trueSize.toNat
    if !BitVec.ult lhs.val rhs.val then
      return (#[], some (.branch (operands.extract 2 (trueSize + 2)) destTrue))
    else
      return (#[], some (.branch (operands.extract (trueSize + 2) operands.size) destFalse))
  | .beqz => do
    let [destTrue, destFalse] := blockOperands.toList | none
    let some (RuntimeValue.reg cond) := operands[0]? | none
    let some trueSize := properties.operandSegmentSizes.values[1]? | none
    let trueSize := trueSize.toNat
    if cond.val = 0#64 then
      return (#[], some (.branch (operands.extract 1 (trueSize + 1)) destTrue))
    else
      return (#[], some (.branch (operands.extract (trueSize + 1) operands.size) destFalse))
  | .bnez => do
    let [destTrue, destFalse] := blockOperands.toList | none
    let some (RuntimeValue.reg cond) := operands[0]? | none
    let some trueSize := properties.operandSegmentSizes.values[1]? | none
    let trueSize := trueSize.toNat
    if cond.val ≠ 0#64 then
      return (#[], some (.branch (operands.extract 1 (trueSize + 1)) destTrue))
    else
      return (#[], some (.branch (operands.extract (trueSize + 1) operands.size) destFalse))

def Rv64.interpretOp' (opType : Veir.Rv64) (properties : propertiesOf opType)
    (resultTypes : Array TypeAttr) (_operands : Array RuntimeValue) (_blockOperands : Array BlockPtr)
    : Option ((Array RuntimeValue) × Option ControlFlowAction) :=
  match opType with
  | .get_register => do
    let [⟨.registerType reg, _⟩] := resultTypes.toList | none
    if reg.index = some 0 then
      return (#[.reg ⟨0⟩], none)
    else
      none

def Cf.interpretOp' (opType : Veir.Cf) (properties : propertiesOf opType)
    (_resultTypes : Array TypeAttr) (operands : Array RuntimeValue) (blockOperands : Array BlockPtr)
    : Interp ((Array RuntimeValue) × Option ControlFlowAction) :=
  match opType with
  | .br => do
    let [dest] := blockOperands.toList | none
    return (#[], some (.branch operands dest))
  | .cond_br => do
    let [destTrue, destFalse] := blockOperands.toList | none
    let some condVal := operands[0]? | none
    let some (trueSizeInt : Int) := properties.operandSegmentSizes.values[1]? | none
    let trueSize := trueSizeInt.toNat
    match condVal with
    | .int 1 (.val cond) =>
      if cond = 1#1 then
        return (#[], some (.branch (operands.extract 1 (trueSize + 1)) destTrue))
      else
        return (#[], some (.branch (operands.extract (trueSize + 1) operands.size) destFalse))
    | .int 1 .poison => Interp.ub
    | _ => none

def Comb.interpretOp' (opType : Veir.Comb) (properties : propertiesOf opType)
    (operands : Array RuntimeValue) (_blockOperands : Array BlockPtr)
    : Option ((Array RuntimeValue) × Option ControlFlowAction) :=
  match opType with
  | .add => do
    let l : List _ := operands.toList
    let .int w fst := l[0]! | none
    let some nl := l.mapM (
        fun e => do
          let .int w' val := e | none
          if h : w' ≠ w then none else
          return val.cast (by simpa using h)) | none
    return (#[.int w (Veir.Data.Comb.add nl)], none)
  | _ => none

def HW.interpretOp' (opType : Veir.HW) (properties : propertiesOf opType)
    (resultTypes : Array TypeAttr) (_blockOperands : Array BlockPtr)
    : Option ((Array RuntimeValue) × Option ControlFlowAction) :=
  match opType with
  | .constant => do
    let resType ← resultTypes[0]?
    let .integerType bw := resType.val
      | none
    return (#[.int bw.bitwidth
      (.val (Veir.Data.HW.constant (BitVec.ofInt bw.bitwidth properties.value.value)).val)], none)
  | _ => none
/--
  Interpret a single operation given its opcode, type-dependent properties,
  result types, and the runtime values of its operands.
  Return the result runtime values and an optional control flow action indicating how
  to continue the interpretation.
  If any error occurs during interpretation (e.g., unknown operation, missing variable),
  returns `none`.
-/
def interpretOp' (opType : OpCode) (properties : propertiesOf opType)
    (resultTypes : Array TypeAttr) (operands : Array RuntimeValue) (blockOperands : Array BlockPtr)
    (mem : MemoryState)
    : Interp ((Array RuntimeValue) × MemoryState × Option ControlFlowAction) :=
  match opType with
  | .arith arithOp => do
    let (vals, act) ← Arith.interpretOp' arithOp properties resultTypes operands blockOperands
    return (vals, mem, act)
  | .mod_arith modArithOp => do
    let (vals, act) ← ModArith.interpretOp' modArithOp properties resultTypes operands blockOperands
    return (vals, mem, act)
  | .felt feltOp => do
    let (vals, act) ← Felt.interpretOp' feltOp properties resultTypes operands blockOperands
    return (vals, mem, act)
  | .llvm llvmOp => do
    Llvm.interpretOp' llvmOp properties resultTypes operands blockOperands mem
  | .riscv riscvOp => do
    Riscv.interpretOp' riscvOp properties resultTypes operands blockOperands mem
  | .riscv_cf riscvCfOp => do
    let (vals, act) ← Riscv_Cf.interpretOp' riscvCfOp properties resultTypes operands blockOperands
    return (vals, mem, act)
  | .riscv_stack riscvStackOp =>
    Riscv_Stack.interpretOp' riscvStackOp properties resultTypes operands blockOperands mem
  | .rv64 rv64Op => do
    let (vals, act) ← Rv64.interpretOp' rv64Op properties resultTypes operands blockOperands
    return (vals, mem, act)
  | .cf cfOp => do
    let (vals, act) ← Cf.interpretOp' cfOp properties resultTypes operands blockOperands
    return (vals, mem, act)
  | .comb combOp => do
    let (vals, act) ← Comb.interpretOp' combOp properties operands blockOperands
    return (vals, mem, act)
  | .hw hwOp => do
    let (vals, act) ← HW.interpretOp' hwOp properties resultTypes blockOperands
    return (vals, mem, act)
  | .func .return => do
    return (#[], mem, some (.return operands))
  | .cir .return => do
    return (#[], mem, some (.return operands))
  | .builtin .unrealized_conversion_cast => do
    let some resType := resultTypes[0]? | none
    match resType.val, operands.toList with
    | .registerType _, [.int _bw val] =>
      return (#[.reg (LLVM.Int.toReg val)], mem, none)
    | .registerType _, [.byte _bw val] =>
      return (#[.reg (LLVM.Byte.toReg val)], mem, none)
    | .registerType _, [.addr val] =>
      return (#[.reg ⟨val.toNat⟩], mem, none)
    | .integerType _bw, [.reg val] =>
      let .integerType resBw := resType.val | none
      return (#[.int resBw.bitwidth (RISCV.Reg.toInt val resBw.bitwidth)], mem, none)
    | .byteType _bw, [.reg val] =>
      let .byteType resBw := resType.val | none
      return (#[.byte resBw.bitwidth (RISCV.Reg.toByte val resBw.bitwidth)], mem, none)
    | .llvmPointerType _, [.reg val] =>
      return (#[.addr ⟨val.val⟩], mem, none)
    | _ , _ => none
  | _ => none

/-- Wrapper around `interpretOp'` that retrieves the operation type, properties,
result types, and successor blocks from the operation pointer. -/
abbrev OperationPtr.interpret (op : OperationPtr) (ctx : IRContext OpCode)
    (operandValues : Array RuntimeValue) (memory : MemoryState) :=
    interpretOp' (op.getOpType! ctx) (op.getProperties! ctx (op.getOpType! ctx))
    (op.getResultTypes! ctx) operandValues (op.getSuccessors! ctx) memory

/--
  Interpret a single operation given the current interpreter state.
  Return an updated interpreter state and a control flow action indicating how
  to continue the interpretation.
  If any error occurs during interpretation (e.g., unknown operation, missing variable),
  return `none`.
-/
@[expose]
def interpretOp (op : OperationPtr) {ctx : WfIRContext OpCode} (state : InterpreterState ctx)
    (inBounds : op.InBounds ctx.raw := by grind)
    : Interp (InterpreterState ctx × Option ControlFlowAction) := do
  let some operands := state.variables.getOperandValues op | none
  let (resultValues, mem, action) ← op.interpret ctx operands state.memory
  let newVars ← state.variables.setResultValues? op resultValues
  let newState := ⟨newVars, mem⟩
  return (newState, action)

/--
  Interpret a chain of operations, starting from the given operation pointer.
  Continue to interpret operations until a terminator is encountered,
  or the end of the block is reached.
  Return a ControlFlowAction indicating how to continue the interpretation.
  Return `none` if any errors occur during interpretation.
-/
def interpretOpChain (op : OperationPtr) {ctx : WfIRContext OpCode} (state : InterpreterState ctx)
    (opInBounds : op.InBounds ctx.raw := by grind)
    : Interp (InterpreterState ctx × ControlFlowAction) := do
  let (state, action) ← interpretOp op state
  match action with
  | none =>
    rlet next ← (op.get ctx.raw).next
    interpretOpChain next state
  | some action =>
    return (state, action)
termination_by op.idxInParentFromTail ctx.raw
decreasing_by grind

/--
  Interpret a list of operations passed as a `List`, stopping at the first terminator.
  Return the new interpreter state, and an optional control flow action indicating how to
  continue the interpretation, with an absent control flow action indicating that the end of the
  list was reached without encountering a terminator.
  Return `none` if any errors occur during interpretation.
-/
def interpretOpList {ctx : WfIRContext OpCode} (ops : List OperationPtr)
    (state : InterpreterState ctx)
    (opInBounds : ∀ op ∈ ops, op.InBounds ctx.raw := by grind)
    : Interp (InterpreterState ctx × Option ControlFlowAction) :=
  match ops with
  | [] => return (state, none)
  | op :: ops => do
    let (state, action) ← interpretOp op state
    match action with
    | none => interpretOpList ops state (by grind)
    | some cf => return (state, cf)

/--
  Interpret a list of operations passed as a `List`, stopping at the first terminator.
  Return the new interpreter state, and a control flow action indicating how to continue the
  interpretation. If no terminator is encountered, return `none`.
  Return `none` if any errors occur during interpretation.
-/
@[expose]
def interpretTerminatedOpList {ctx : WfIRContext OpCode} (ops : List OperationPtr)
    (state : InterpreterState ctx)
    (opInBounds : ∀ op ∈ ops, op.InBounds ctx.raw := by grind)
    : Interp (InterpreterState ctx × ControlFlowAction) := do
  match ← interpretOpList ops state opInBounds with
  | (_, none) => none
  | (state, some cf) => return (state, cf)

/--
  Interpret a block of operations, starting from the first operation in the block.
  The block arguments are set from `values` before interpreting the operations.
  Return the resulting interpreter state and a ControlFlowAction indicating how
  to continue the interpretation.
  Return `none` if any errors occur during interpretation.
-/
def interpretBlock (blockPtr : BlockPtr) (values : Array RuntimeValue) {ctx : WfIRContext OpCode}
    (state : InterpreterState ctx) (blockInBounds : blockPtr.InBounds ctx.raw := by grind) :
    Interp (InterpreterState ctx × ControlFlowAction) := do
  let newVars ← state.variables.setArgumentValues? blockPtr values
  let state := ⟨newVars, state.memory⟩
  rlet firstOp ← (blockPtr.get ctx.raw).firstOp
  interpretOpChain firstOp state

/--
  Interpret a CFG, starting from the given block.
  The arguments of the starting block are set from `values`.
  Return the resulting interpreter state and values eventually returned, if any.
  Return `none` if any errors occur during interpretation.
-/
def interpretBlockCFG (blockPtr : BlockPtr) (values : Array RuntimeValue) {ctx : WfIRContext OpCode}
    (state : InterpreterState ctx) (blockInBounds : blockPtr.InBounds ctx.raw := by grind) :
    Interp (InterpreterState ctx × Array RuntimeValue) := do
  match interpretBlock blockPtr values state blockInBounds with
  | .ok (state, .return res) => .ok (state, res)
  | .ok (state, .branch res succ) =>
    if h : succ.InBounds ctx.raw then
      interpretBlockCFG succ res state h
    else
      .fail
  | .ub => .ub
  | .fail => .fail
partial_fixpoint

/--
  Interpret a region, starting from its first block.
  The arguments of the first block are set from `values`.
  Return the resulting interpreter state and values eventually returned, or `none`
  if any errors occur during interpretation.
-/
def interpretRegion (region : RegionPtr) (values : Array RuntimeValue) {ctx : WfIRContext OpCode}
    (state : InterpreterState ctx) (regionIn : region.InBounds ctx.raw := by grind) :
    Interp (InterpreterState ctx × Array RuntimeValue) := do
  rlet block ← (region.get ctx.raw).firstBlock
  interpretBlockCFG block values state

/--
  Interpret an operation representing a function, given the runtime values of its arguments
  and the current memory state. Return the resulting memory state and the values eventually
  returned.

  Unlike the other interpreter functions, this does not take an `InterpreterState`:
  a function call starts with a fresh, empty variable state, since the caller's SSA
  values are not visible inside the callee.
-/
def interpretFunction (op : OperationPtr) (values : Array RuntimeValue) {ctx : WfIRContext OpCode}
    (mem : MemoryState) (opIn : op.InBounds ctx.raw := by grind) :
    Interp (MemoryState × Array RuntimeValue) := do
  if h : op.getNumRegions ctx.raw ≠ 1 then
    none
  else
    let state : InterpreterState ctx := ⟨.empty ctx, mem⟩
    let (state, results) ← interpretRegion (FunctionOpInterface.getFunctionBody op ctx.raw) values state
    return (state.memory, results)

/--
  Interpret a builtin.module operation.
  This is done by interpreting the unique region of the operation.
  Return the values eventually returned, or `none` if any errors occur during interpretation.
-/
def interpretModule (ctx : WfIRContext OpCode) (op : OperationPtr)
    (opIn : op.InBounds ctx.raw := by grind) : Interp (Array RuntimeValue) := do
  if h: op.getNumRegions ctx.raw ≠ 1 then
    none
  else
    let (_state, results) ← interpretRegion (op.getRegion ctx.raw 0) #[] (InterpreterState.empty ctx)
    return results

end Veir
